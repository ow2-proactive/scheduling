apply plugin: 'war'

repositories {
    mavenCentral()
}

configurations.providedCompile.transitive = true

configurations {
    all*.exclude group: 'com.zaxxer', module: 'HikariCP-java6'
}

task deleteConfigFiles(type: Delete){
    delete 'build/classes/main/config'
    delete 'build/classes/main/dist'
}

task copyConfigFiles(type: Copy) {
    from project(':').files('config')
    into 'build/classes/main/config'
}

task copyDistFiles(type: Copy) {
    from project(':').files('dist/boot')
    into 'build/classes/main/dist/boot'
}

copyConfigFiles.dependsOn deleteConfigFiles
test.dependsOn copyConfigFiles, copyDistFiles

dependencies {

    providedCompile project(':scheduler:scheduler-server')
    providedCompile group: 'com.zaxxer', name: 'HikariCP', version: '2.6.0'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.7.21'
    testCompile group: 'org.powermock', name: 'powermock-module-junit4', version: '1.7.0RC4'
    testCompile group: 'org.powermock', name: 'powermock-api-mockito2', version: '1.7.0RC4'

}

sourceSets {
    main.compileClasspath += configurations.providedCompile
    main.runtimeClasspath += configurations.providedCompile
    test.compileClasspath += configurations.providedCompile
    test.runtimeClasspath += configurations.providedCompile
}

war {

    doFirst {

        into ('WEB-INF/classes/config'){
            from project(':').files('config', '*')
        }

        into('WEB-INF/classes/dist/lib') {
            from { configurations.providedCompile}
        }

        into('WEB-INF/classes/dist/boot') {
            from project(':').files('dist/boot', '*')
        }

        manifest {
            attributes 'Implementation-Title': 'Proactive Server Web App',
                    'Implementation-Version': "${schedulingVersion}",
                    'Class-Path': configurations.providedCompile.collect() {
                        if (it.getName().endsWith('.jar')) return ('lib/'.concat(it.getName())) else return ''
                    }.join(' ')
        }

    }

    archiveName "proactive-server.war"

}
