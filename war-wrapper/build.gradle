apply plugin: 'war'

repositories {
    mavenCentral()
}

configurations.compile.transitive = true

configurations {
    all*.exclude group: 'com.zaxxer', module: 'HikariCP-java6'
}

task deleteConfigFiles(type: Delete){
    delete 'src/main/resources/config'
}

task copyConfigFiles(type: Copy) {
    from project(':').files('config', '*')
    into 'src/main/resources/config'
}

copyConfigFiles.dependsOn deleteConfigFiles

war.dependsOn copyConfigFiles

dependencies {

    providedCompile project(':scheduler:scheduler-server')
    providedCompile group: 'com.zaxxer', name: 'HikariCP', version: '2.6.0'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.7.21'
    testCompile group: 'org.powermock', name: 'powermock-module-junit4', version: '1.7.0RC4'
    testCompile group: 'org.powermock', name: 'powermock-api-mockito2', version: '1.7.0RC4'

}

sourceSets.main.runtimeClasspath += configurations.providedCompile
sourceSets.test.compileClasspath += configurations.providedCompile
sourceSets.test.runtimeClasspath += configurations.providedCompile

war {

    doFirst {

        into('WEB-INF/classes/dist/lib') {
            from { configurations.providedCompile}
        }

        manifest {
            attributes 'Implementation-Title': 'Proactive Server Web App',
                    'Implementation-Version': "${schedulingVersion}",
                    'Class-Path': configurations.providedCompile.collect() {
                        if (it.getName().endsWith('.jar')) return ('lib/'.concat(it.getName())) else return ''
                    }.join(' ')
        }

    }

    archiveName "proactive-server.war"

}
