apply plugin: 'java-library-distribution'
apply plugin: 'gradle-one-jar'


buildscript {
    repositories {
        if (project.hasProperty('local')) {
            mavenLocal()
        }
    mavenCentral()
        jcenter()
        maven { url 'http://repository.activeeon.com/content/groups/proactive/' }
    }
}



dependencies {
    compile(
            "org.objectweb.proactive:programming-extension-processbuilder:${programmingVersion}",
            "org.objectweb.proactive:programming-extension-dataspaces:${programmingVersion}",
            "org.ow2.proactive:emailnotification-addons:${schedulingVersion}",

            project(':scheduler:scheduler-client'),
            project(':rest:rest-client'),
            project(':rm:rm-node'),
    )

    runtime (
            "org.objectweb.proactive:programming-extension-pamr:${programmingVersion}",
            "org.objectweb.proactive:programming-extension-pnp:${programmingVersion}",
            "org.objectweb.proactive:programming-extension-pnpssl:${programmingVersion}",
            project(':scheduler:scheduler-examples')
    )

    runtime fileTree(dir: project.nativeLibsDir, exclude: '*.jar')

    testCompile project(':scheduler:scheduler-examples')
    testCompile project(':common:common-api').sourceSets.test.output
}

task stub(type: StubTask) {
    classes = [
            'org.ow2.proactive.scheduler.common.TaskTerminateNotification',
            'org.ow2.proactive.scheduler.task.TaskLauncher'
    ]
}
serialver.dependsOn stub

test {
    exclude 'functionaltests/**'
    exclude 'unittests/**'
    exclude 'unitTests/**'
    exclude 'performancetests/**'

    if(Boolean.valueOf(System.getProperty("java.awt.headless"))){
        exclude 'org/ow2/proactive_grid_cloud_portal/cli/**'
    }
    systemProperties << ['java.security.policy': file("$rootDir/config/security.java.policy-server").absolutePath]
    systemProperties << ['file.encoding': 'UTF-8']
    systemProperties << ['proactive.home': rootDir.absolutePath]
}

task('functionalTest', type: Test).configure rootProject.schedulingFunctionalTestConfiguration

ext.oneJarManifest = manifest {
    attributes 'One-Jar-URL-Factory': 'com.simontuffs.onejar.JarClassLoader$OneJarURLFactory'
    attributes 'One-Jar-Expand': 'lib,binlib,main' // expand jars for subprocesses (forked tasks)
}

task standaloneJar(type: OneJar) {
    logging.captureStandardOutput LogLevel.INFO
    mainClass = 'org.ow2.proactive.resourcemanager.utils.RMNodeStarter'
    binLib = files({ rootProject.configurations.nativeBundle.collect { zipTree(it) } })
    additionalDir = file('src/main/resources')
    manifest = oneJarManifest
}
build.dependsOn standaloneJar
artifacts {
    archives standaloneJar
}
