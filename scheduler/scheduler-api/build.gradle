dependencies {
    compile("org.objectweb.proactive:programming-extension-dataspaces-common:${programmingVersion}")

    compile 'it.sauronsoftware.cron4j:cron4j:2.2.5'
    compile 'org.apache.commons:commons-collections4:4.0'
    compile 'msv:isorelax:20050913'
    compile 'msv:msv:20050913'
    compile 'commons-io:commons-io:2.5'
    compile group: 'org.springframework', name: 'spring-expression', version: '4.2.5.RELEASE'

    compile 'joda-time:joda-time:2.9.1'

    compile project(':common:common-api')

    runtime 'com.fasterxml.woodstox:woodstox-core:5.0.1'
    runtime 'msv:relaxngDatatype:20050913'
    runtime 'msv:xsdlib:20050913'
    runtime 'xerces:xercesImpl:2.11.0'

    testCompile project (':common:common-api').sourceSets.test.output
}

apply plugin: 'trang'
task convertSchemas
['3.0', '3.1', '3.2', '3.3', '3.4', '3.5', '3.6', '3.7', '3.8', '3.9', '3.10', '3.11', 'dev'].each { schemaVersion ->
    task("convertSchemasXsd-$schemaVersion", type: org.hsudbrock.tranggradleplugin.TrangTask) {
        sourceDirectory = project.file("src/main/resources/org/ow2/proactive/scheduler/common/xml/schemas/jobdescriptor/${schemaVersion}")
        targetDirectory = sourceDirectory
        doLast {
            project.delete project.file("src/main/resources/org/ow2/proactive/scheduler/common/xml/schemas/jobdescriptor/${schemaVersion}/xsi.xsd")
        }
    }
    task("convertSchemasRng-$schemaVersion", type: org.hsudbrock.tranggradleplugin.TrangTask) {
        sourceDirectory = project.file("src/main/resources/org/ow2/proactive/scheduler/common/xml/schemas/jobdescriptor/${schemaVersion}")
        targetDirectory = sourceDirectory
        targetExtension = 'rng'
        doLast {
            project.delete project.file("src/main/resources/org/ow2/proactive/scheduler/common/xml/schemas/jobdescriptor/${schemaVersion}/xsi.xsd")
        }
    }
    task("convertSchemas-$schemaVersion").dependsOn << ["convertSchemasXsd-$schemaVersion", "convertSchemasRng-$schemaVersion"]
    convertSchemas.dependsOn << "convertSchemasXsd-$schemaVersion"
    convertSchemas.dependsOn << "convertSchemasRng-$schemaVersion"
}

task stub(type: StubTask) {
    classes = ['org.ow2.proactive.scheduler.common.Scheduler', 'org.ow2.proactive.scheduler.synchronization.SynchronizationInternal']
}
serialver.dependsOn stub
