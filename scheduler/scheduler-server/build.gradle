repositories {
    if (project.hasProperty('local')) mavenLocal()

    maven {
        url "http://repository.activeeon.com/content/groups/proactive/"
    }
}

apply plugin: 'java-library-distribution'

dependencies {
    compile 'commons-cli:commons-cli:1.3.1'
    compile 'commons-io:commons-io:2.5'
    compile 'javax.mail:mail:1.4.7'
    compile 'joda-time:joda-time:2.9.1'
    compile 'org.apache.commons:commons-lang3:3.4'

    compile "org.ow2.proactive:emailnotification-addons:${schedulingVersion}"

    compile 'org.eclipse.jetty:jetty-webapp:9.2.14.v20151106'
    compile "org.objectweb.proactive:programming-core:${programmingVersion}"

    compile project(':common:common-api')
    compile project(':common:common-client')
    compile project(':common:common-db')
    compile project(':common:common-http')
    compile project(':scheduler:scheduler-api')
    compile project(':scheduler:scheduler-client')
    compile project(':scheduler:scheduler-node')
    compile project(':rm:rm-server')
    compile project(':rm:rm-client')




    testCompile "com.google.jimfs:jimfs:1.1"
    testCompile "org.objectweb.proactive:programming-extension-pnp:${programmingVersion}"
    testCompile "org.objectweb.proactive:programming-extension-pnpssl:${programmingVersion}"
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'org.hamcrest:hamcrest-all:1.3'

    testCompile 'org.codehaus.groovy:groovy-all:2.4.12'

    testCompile 'org.jvnet.winp:winp:1.24'
    testCompile files("${System.properties['java.home']}/../lib/tools.jar")

    testCompile project(':rm:rm-server').sourceSets.test.output // to get shared test classes
    testCompile project(':common:common-api').sourceSets.test.output
    testCompile project(':rm:rm-policy:rm-policy-scheduler')
    testCompile project(':scheduler:scheduler-examples')

    testCompile 'org.apache.sshd:sshd-core:0.14.0'


    runtime 'org.hsqldb:hsqldb:2.4.0'

    runtime "org.objectweb.proactive:programming-extension-pnp:${programmingVersion}"
    runtime "org.objectweb.proactive:programming-extension-pnpssl:${programmingVersion}"
    runtime "org.objectweb.proactive:programming-extension-pamr:${programmingVersion}"

}

task('functionalTest', type: Test).configure schedulingFunctionalTestConfiguration
task('performanceTest', type: Test).configure schedulingPerformanceTestConfiguration

task verifyPerformanceChange(type: Test){
    systemProperties << ['performanceTestThreshold': System.getProperty('performanceTestThreshold')]

    systemProperties << ['pa.rm.home': rootDir.absolutePath]
    systemProperties << ['pa.scheduler.home': rootDir.absolutePath]
    systemProperties << ['java.security.policy': file("$rootDir/config/security.java.policy-server").absolutePath]
    systemProperties << ['file.encoding': 'UTF-8']

    include 'performancetests/verification/**'

}

task testJar(type: Jar) {
    classifier = 'tests'
    from sourceSets.test.output
}


task stub(type: StubTask) {
    classes = [
            'org.ow2.proactive.scheduler.authentication.SchedulerAuthentication',
            'org.ow2.proactive.scheduler.core.rmproxies.RMProxyActiveObject',
            'org.ow2.proactive.scheduler.core.SchedulerFrontend',
            'org.ow2.proactive.scheduler.core.TerminateNotification'
    ]
}
serialver.dependsOn stub

rootProject.dist.dependsOn testJar

task dist(type: Copy) {
    from testJar
    into file("$rootDir/dist/lib")
}

dist.dependsOn project(':rm:rm-server').dist

artifacts {
    archives testJar
}

functionalTest.dependsOn rootProject.dist, dist
performanceTest.dependsOn rootProject.dist, dist
clean.dependsOn rootProject.cleanDist
